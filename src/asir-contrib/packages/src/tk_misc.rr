/* $OpenXM$ 
  old file name is misc.rr
*/

Misc_ox_work_dir_created = 0$
Misc_ox_file_name = 0$
def get_pid() {
  return getpid();
}
/* ox_work_dir() returns
   ~/OpenXM_tmp/pid
   If this directory does not exists, it creates the directory.
   If the environmental variable OpenXM_tmp is set, the value
   will be used.
*/
def ox_work_dir() {
  extern Misc_ox_work_dir_created;
  if (ox_getenv("OpenXM_tmp") == 0) {
     Work_home = ox_getenv("HOME")+"/OpenXM_tmp";
  }else{
     Work_home = ox_getenv("OpenXM_tmp");
  }
  Work = Work_home+"/"+rtostr(get_pid());

  if (!Misc_ox_work_dir_created) {
    /* Unix dependent code to create a directory. */
    if (shell("test -d "+Work_home) != 0) {
      print("Working directory "+Work_home+" will be created.");
      if (shell("mkdir "+Work_home) != 0) {
         error("Could not create the directory "+Work_home);
      }
    }
    if (shell("test -d "+Work) != 0) {
      print("Working directory "+Work+" will be created.");
      if (shell("mkdir "+Work) != 0) {
         error("Could not create the directory "+Work);
      }
    }
    Misc_ox_work_dir_created = 1;
  }
  return Work;
}

def ox_remove_work_dir() {
  if (ox_getenv("OpenXM_tmp") == 0) {
     Work_home = ox_getenv("HOME")+"/OpenXM_tmp";
  }else{
     Work_home = ox_getenv("OpenXM_tmp");
     /* Safety precaution. */
     if (Work_home == ox_getenv("HOME") ||
         Work_home == "/" ||
         Work_home == "" ||
         Work_home == "." ||
         Work_home == ".." ) {
         error("ox_remove_work_dir():  Work_home seems to be wrong.");
     }
  }
  if (shell("test -d "+Work_home) == 0) {
     shell("rm -rf "+Work_home+" [0-9]*");
  }
  return Work_home;
}

def ox_new_file_name() {
  extern Misc_ox_file_name;
  Name = "work"+rtostr(Misc_ox_file_name);
  Misc_ox_file_name++;
  return Name;
}
end$
