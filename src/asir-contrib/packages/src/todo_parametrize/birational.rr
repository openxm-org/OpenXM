/* $OpenXM$ */

/*
 A=u0*P+u1*Q+u2*R.

 (u:v:w)=(P(x,y,z):Q(x,y,z):R(x,y,z)).

 H=(a0*P^M+a1*P^{M-1}*R+...+aM*R^M)*R
   +(b0*P^M*+b1*P^{M-1}*R+...+bM*P^M)*Q]*z
   +(c0*P^M+c1*P^{M-1}*R+...+cM*R^M)R*x.

 x/z=-[(a0*u^M+a1*u^{M-1}*w+...+aM*w^M)*w
   +(b0*u^M*+b1*u^{M-1}*w+...+bM*w^M)*v]
   /(c0*u^M+c1*u^{M-1}*w+...+cM*w^M)*w.

 H'=(a0*P^M+a1*P^{M-1}*R+...+aM*R^M)*R
   +(b0*P^M*+b1*P^{M-1}*R+...+bM*P^M)*Q]*z
   +(c0*P^M+c1*P^{M-1}*R+...+cM*R^M)R*y.

 x/y=-[(a0*u^M+a1*u^{M-1}*w+...+aM*w^M)*w
   +(b0*u^M*+b1*u^{M-1}*w+...+bM*w^M)*v]
   /(c0*u^M+c1*u^{M-1}*w+...+cM*w^M)*w.
*/

def birat3(F,A){
    Vars=exclude_xyz(vars(A));
    P=coef(A,1,Vars[0]);
    Q=coef(A,1,Vars[1]);
    R=coef(A,1,Vars[2]);

    M=2+tdeg(F);

    for(H1=0,I=0; I<=M; I++)
	H1=H1+strtov("a"+rtostr(I))*u^(M-I)*w^I;

    for(H2=H1,H3=H1,I=0; I<=M; I++){
	H2=subst(H2,strtov("a"+rtostr(I)),strtov("b"+rtostr(I)));
	H3=subst(H3,strtov("a"+rtostr(I)),strtov("c"+rtostr(I)));
    }

    H=-(H1*w+H2*v)/(H3*w);

    Equx=[]; Equy=[];
    Pts_=intersect(F,R);
    Zero=0;
    Pole=3*M;	/* "H の極の位数の和" < Pole */
    for(T=1; Zero<=Pole; T++){
	L=y-T*x;
	Pts=intersect(F,L);
	Pts=exclude(Pts,Pts_); K=length(Pts);
	for(J=0; J<K; J++){
	    Pt=Pts[J];

	    P_=simpalg(subst(P,x,Pt[0],y,Pt[1],z,Pt[2]));
	    Q_=simpalg(subst(Q,x,Pt[0],y,Pt[1],z,Pt[2]));
	    R_=simpalg(subst(R,x,Pt[0],y,Pt[1],z,Pt[2]));

	    for(PI=[1],I=0; I<M; I++)
		PI=cons(simpalg(P_*PI[0]),PI);

	    for(RI=[1],I=0; I<M; I++)
		RI=cons(simpalg(R_*RI[0]),RI);

	    RI=reverse(RI);

	    for(H1_=0,I=0; I<=M; I++)
		H1_=H1_+strtov("a"+rtostr(I))*simpalg(PI[I]*RI[I]);

	    for(H2_=H1_,H3_=H1_,I=0; I<=M; I++){
		H2_=subst(H2_,strtov("a"+rtostr(I)),strtov("b"+rtostr(I)));
		H3_=subst(H3_,strtov("a"+rtostr(I)),strtov("c"+rtostr(I)));
	    }

	    Hx_=algptorat(simpalg((H1_*R_+H2_*Q_)*Pt[2]+H3_*R_*Pt[0]));
	    Hy_=algptorat(simpalg((H1_*R_+H2_*Q_)*Pt[2]+H3_*R_*Pt[1]));

	    if(algs(Pt) != []){
		V=algptorat(algs(Pt)[0]);
		Equx=append(coefs(Hx_,[V]),Equx);
		Equy=append(coefs(Hy_,[V]),Equy);
	    }else{
		Equx=cons(ptozp(Hx_),Equx);
		Equy=cons(ptozp(Hy_),Equy);
	    }

	    Zero=Zero+number(Pt);
	    Pts_=cons(Pt,Pts_);
	}
    }

    Vars=vars_(Equx);
    Gr=td_gr(Equx,Vars,2); L=length(Gr);

    for(X=H,J=L-1; J>=0; J--){
	E=Gr[J]; V=var(E);
	X=subst(X,V,lzero(E,V));
    }

    Vars=vars_(Equy);
    Gr=td_gr(Equy,Vars,2); L=length(Gr);

    for(Y=H,J=L-1; J>=0; J--){
	E=Gr[J]; V=var(E);
	Y=subst(Y,V,lzero(E,V));
    }

    X=subst(X,u,x,v,y,w,z);
    Y=subst(Y,u,x,v,y,w,z);

    return(aftopro([X,Y]));
}

def birat3_gr(F,A){
    F=subst(F,z,1);
    A=subst(A,z,1);

    Vars=exclude_xyz(vars(A));
    P=coef(A,1,Vars[0]);
    Q=coef(A,1,Vars[1]);
    R=coef(A,1,Vars[2]);

    Gr=td_gr([F,R*u-P,R*v-Q,R*k-1],[k,y,x,u,v],2);
    L=length(Gr);

    for(I=0; I<L; I++)
	if(deg(Gr[I],x)==1){
	    X=lzero(Gr[I],x);
	    break;
	}

    for( ; I<L; I++)
	if(deg(Gr[I],y)==1){
	    Y=lzero(Gr[I],y);
	    if(var(Gr[I])==x)
		Y=subst(Y,x,X);
	    break;
	}

    X=subst(red(X),u,x,v,y);
    Y=subst(red(Y),u,x,v,y);

    return([X,Y]);
}

/*
 A=P+Q*t (t=-P/Q)
 (a0*P^n+a1*P^{n-1}*Q+...+an*Q^n)*z
 +(b0*P^n+b1*P^{n-1}*Q+...+bn*Q^n)*x=0  (n=deg(F))

 x/z=-(a0*(-t)^n+a1*(-t)^{n-1}+...+an)
	/(b0*(-t)^n+b1*(-t)^{n-1}+...+bn)
*/

def birat2(F,A){
    Vars=exclude_xyz(vars(A));
    P=coef(A,0,t);
    Q=coef(A,1,t);

    M=tdeg(F);

    for(H1=0,I=0; I<=M; I++)
	H1=H1+strtov("a"+rtostr(I))*(-t)^(M-I);

    for(H2=H1,I=0; I<=M; I++)
	H2=subst(H2,strtov("a"+rtostr(I)),strtov("b"+rtostr(I)));

    H=-H1/H2;

    Equx=[]; Equy=[];
    Pts_=intersect(F,P);
    Zero=0;
    Pole=2*M;	/* "H の極の位数の和" < Pole */
    for(T=1; Zero<=Pole; T++){
	L=y-T*x;
	Pts=intersect(F,L);
	Pts=exclude(Pts,Pts_); K=length(Pts);
	for(J=0; J<K; J++){
	    Pt=Pts[J];

	    P_=simpalg(subst(P,x,Pt[0],y,Pt[1],z,Pt[2]));
	    Q_=simpalg(subst(Q,x,Pt[0],y,Pt[1],z,Pt[2]));

	    for(PI=[1],I=0; I<M; I++)
		PI=cons(simpalg(P_*PI[0]),PI);

	    for(QI=[1],I=0; I<M; I++)
		QI=cons(simpalg(Q_*QI[0]),QI);

	    QI=reverse(QI);

	    for(H1_=0,I=0; I<=M; I++)
		H1_=H1_+strtov("a"+rtostr(I))*simpalg(PI[I]*QI[I]);

	    for(H2_=H1_,I=0; I<=M; I++)
		H2_=subst(H2_,strtov("a"+rtostr(I)),strtov("b"+rtostr(I)));

	    Hx_=algptorat(simpalg(H1_*Pt[2]+H2_*Pt[0]));
	    Hy_=algptorat(simpalg(H1_*Pt[2]+H2_*Pt[1]));

	    if(algs(Pt) != []){
		V=algptorat(algs(Pt)[0]);
		Equx=append(coefs(Hx_,[V]),Equx);
		Equy=append(coefs(Hy_,[V]),Equy);
	    }else{
		Equx=cons(ptozp(Hx_),Equx);
		Equy=cons(ptozp(Hy_),Equy);
	    }

	    Zero=Zero+number(Pt);
	    Pts_=cons(Pt,Pts_);
	}
    }

    Vars=vars_(Equx);
    Gr=td_gr(Equx,Vars,2); L=length(Gr);

    for(X=H,J=L-1; J>=0; J--){
	E=Gr[J]; V=var(E);
	X=subst(X,V,lzero(E,V));
    }

    Vars=vars_(Equy);
    Gr=td_gr(Equy,Vars,2); L=length(Gr);

    for(Y=H,J=L-1; J>=0; J--){
	E=Gr[J]; V=var(E);
	Y=subst(Y,V,lzero(E,V));
    }

    return(aftopro([X,Y]));
}

def birat2_res(F,Ad){
    XX=fctr(res(y,F,Ad));
    Pxz=factor_search(XX,x,z);

    YY=fctr(res(x,F,Ad));
    Pyz=factor_search(YY,y,z);

    X=red(lzero(subst(Pxz,z,1),x));
    Y=red(lzero(subst(Pyz,z,1),y));
    H=lzero(Ad,t);

    return(aftopro([X,Y]));
}

def birat2_gr(F,A){
    F=subst(F,z,1);
    A=subst(A,z,1);

    P=coef(A,0,t);
    Q=coef(A,1,t);

    Gr=td_gr([F,Q*t-P,Q*k-1],[k,y,x,t],2);
    L=length(Gr);

    for(I=0; I<L; I++)
	if(deg(Gr[I],x)==1){
	    X=lzero(Gr[I],x);
	    break;
	}

    for( ; I<L; I++)
	if(deg(Gr[I],y)==1){
	    Y=lzero(Gr[I],y);
	    if(var(Gr[I])==x)
		Y=subst(Y,x,X);
	    break;
	}

    return([X,Y]);
}

def aftopro(Rat2){
    X=Rat2[0]; Y=Rat2[1];
    LCM=red(dn(X)*dn(Y)/gcd(dn(X),dn(Y)));

    return([red(LCM*X),red(LCM*Y),LCM]);
}
end$
